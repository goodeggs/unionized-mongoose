mongooseFactory = require '../src'
{expect} = require 'chai'
mongoose = require 'mongoose'
moment = require 'moment'

describe 'random field generation', ->

  beforeEach ->
    @instance = null

  describe 'an instance generated by the factory with no inputs', ->
    before ->
      Model = mongoose.model 'Kitten1', mongoose.Schema
        name: { type: String, required: true }
        cutenessPercentile: { type: Number, required: true }
        personality: { type: String, required: true, enum: ['friendly', 'fierce', 'antisocial', 'changeable'] }
        eyeColor: { type: String, default: 'yellow', required: true }
        isHunter: { type: Boolean, required: true }
        bornAt: { type: Date, required: true }
        description: String

      @factory = mongooseFactory Model

    beforeEach (done) ->
      @factory.json (error, @instance) => done error

    it 'has an _id', ->
      expect(@instance._id).to.be.an.instanceOf mongoose.Types.ObjectId

    it 'can generate a string', ->
      expect(@instance.name).to.be.a 'string'

    it 'can generate a number', ->
      expect(@instance.cutenessPercentile).to.be.a 'number'

    it 'can generate within an enum', ->
      expect(@instance.personality in ['friendly', 'fierce', 'antisocial', 'changeable']).to.be.ok

    it 'can generate a boolean', ->
      expect(@instance.isHunter).to.be.a 'boolean'

    it 'can generate a date (default in 2013)', ->
      born = moment(@instance.bornAt)
      expect(born.isAfter  '2012-12-31').to.be.ok
      expect(born.isBefore '2014-01-01').to.be.ok

    it 'will use provided defaults', ->
      expect(@instance.eyeColor).to.equal 'yellow'

    it 'will ignore non-required attributes', ->
      expect(@instance.description).to.be.undefined

  describe 'arrays', ->
    before ->
      Model = mongoose.model 'Kitten2', mongoose.Schema
        paws: [
          nickname: { type: String, required: true }
          clawCount: Number
        ]

      @factory = mongooseFactory Model

    beforeEach (done) ->
      @factory.json (error, @instance) => done error

    it 'can generate an array', ->
      expect(@instance.paws).to.be.an.instanceOf Array

    it 'generates required properties on array elements', ->
      expect(@instance.paws[0]).to.be.a 'object'
      expect(@instance.paws[0]).to.have.property 'nickname'
      expect(@instance.paws[0]).not.to.have.property 'clawCount'

  describe 'deeply-nested attributes', ->
    before ->
      Model = mongoose.model 'Kitten3', mongoose.Schema
        meta:
          owner:
            name: { type: String, required: true }
            age: Number

      @factory = mongooseFactory Model

    beforeEach (done) ->
      @factory.json (error, @instance) => done error

    it 'are generated', ->
      expect(@instance?.meta?.owner?.name).to.have.length.of.at.least 1
      expect(@instance?.meta?.owner?.age).not.to.be.ok


  describe 'an instance generated with inputs', ->
    before ->
      Model = mongoose.model 'Kitten4', mongoose.Schema
        name: { type: String, required: true }
        meta:
          owner:
            name: { type: String, required: true }
            age: Number

      @factory = mongooseFactory Model

    beforeEach (done) ->
      @factory.json {
        name: 'John Doe'
        meta:
          owner:
            age: 30
        'meta.owner.name': 'Joe Shmoe'
      }, (error, @instance) => done error

    it 'respects top-level inputs', ->
      expect(@instance).to.have.property 'name', 'John Doe'

    it 'respects deeply-nested object inputs', ->
      expect(@instance?.meta?.owner?.age).to.equal 30

    it 'respects deeply-nested dot-pathed arguments', ->
      expect(@instance?.meta?.owner?.name).to.equal 'Joe Shmoe'


  describe 'extending factories', ->
    before ->
      Model = mongoose.model 'Kitten5', mongoose.Schema
        name: { type: String, required: true }
        age: { type: Number, required: true }
        description: String

      @factory = mongooseFactory(Model).define (callback) ->
        @set 'name', 'Fluffy'
        callback()

    beforeEach (done) ->
      @factory.json { description: 'Big ball of fluff' }
      , (error, @instance) => done error

    it 'combines default attributes', ->
      expect(@instance).to.have.property 'name', 'Fluffy'
      expect(@instance).to.have.property 'age'
      expect(@instance.age).to.be.a 'number'

    it 'takes inputs', ->
      expect(@instance).to.have.property 'description', 'Big ball of fluff'
